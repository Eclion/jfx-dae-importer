group 'net.eclionast'
version '1.3'

apply plugin: 'java'
apply plugin: 'jacoco'

configurations {
    codacy
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.3"
    }
}

apply plugin: 'org.sonarqube'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:1.0.13'
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.+"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "JFX DAE Importer"
        property "sonar.projectKey", "eclion:jfx-dae-importer"
    }
}

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    ]
}

check.dependsOn jacocoTestReport

